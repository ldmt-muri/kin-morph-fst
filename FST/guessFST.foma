define Vowel [a | e | i | o | u];
define Cons [b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | y | z];

# U~W: 'u' becomes 'w' before vowels
define UtoW u -> w || _ %^ Vowel;

# I~Y: 'i' becomes 'y' before vowels
define ItoY i -> y ||  _ %^ Vowel;

# Reflexive I: The 'i' in the reflexive prefix become 'iy' before vowels
define RefI i -> [i y] || _ %$ Vowel;

# N~H: 'nh' becomes 'mp'
define NHtoMP [n %^ h] -> [m p];

# AI~E: 'ai' becomes 'e' 
define AItoE [a %^ i] -> [e | i];

# VowelHiatus: Delete 'a' before a vowel at morpheme boundaries
define VowelHiatus a -> 0 || _ %^ Vowel;

# NasAssim: 'n' becomes 'm' before bilabials
define NasAssim n -> m || _ %^ [b | f | m | p | v];

# R~D: 'r' becomes 'd' after 'n'
define RtoD r -> d || n %^ _;

# Apposition
define PrepRule [Vowel %&] -> %';

# Special Passive -- if verb stem ends with b, the passive ending is -yw-, not -w-
define BPass %# -> y || b _ w;

# K~G: 'k' usually becomes 'g' if next consonant is c, f, h, p, s, t, or k.
define KtoG k -> [g | k]  ||  _ Vowel %^ [c | f | h | p | s | t | k];

# K~C: 'k' becomes 'c' before y
define KtoC k -> c ||  _ y;

# Y~W: 'y' may go away before 'w' (as often occurs with passive, perfect constructs) (Not entirely sure about this one)
define YW y -> [0 | y] || _ %#;

# T~D: 't' usually becomes 'd' if next consonant is c, f, h, p, s, t, or k.
define TtoD t -> [d | t] ||  _ Vowel %^ [c | f | h | p | s | t | k];

# R~L: 'r' and 'l' are interchangeable in many words of foreign origin. The Lexicon only uses 'r', so this factors that in (This has been commented out due to the huge ambiguity created.)
#define RtoL r -> [r | l];

# Cleanup 1: Remove $ (reflexive tag)
define Cleanup1 %$ -> 0;

# Cleanup 2: Remove # (passive tag)
define Cleanup2 %# -> 0;

# Cleanup 3: Remove & (contraction tag)
define Cleanup3 %& -> 0;

# Cleanup 4: Remove ^ (generic morpheme boundary tag)
define Cleanup4 %^ -> 0;

# Cleanup 5: Remove @ (end of a guessed verb stem)
define Cleanup5 %@ -> 0;

read lexc guessLexc.lexc
define Lexicon;


# A~I~U: If next-to-last syllable of verb stem has a, i, or u as vowel, then suffix will contain i. Otherwise, e.
define Verb $["+V"] .o. Lexicon;
define Lexicon ~$["+V"] .o. Lexicon;
define AIU i -> e || [e | o] [Cons | Cons Cons] %^ _;
define Verb Verb .o. AIU;
define Verb Verb .o. AIU;
define Verb Verb .o. AIU;
define Verb Verb .o. AIU;
define Lexicon Lexicon | Verb;

# Past Habitual: Must have both +HAB.PAST tags in order to work
define PastHab $["+HAB.PAST"] .o. Lexicon;
define PastHab2 $["+HAB.PAST2"] .o. Lexicon;
define Lexicon ~$["+HAB.PAST"] .o. Lexicon;
define Lexicon ~$["+HAB.PAST2"] .o. Lexicon;
define PastHab PastHab & PastHab2;
define Lexicon Lexicon | PastHab;

# Perfect: Only can be perfective aspect if stative, recent, remote, or immediate past tense (and vice versa--those tenses must be perfect)
define Perfect $["+PERF"] .o. Lexicon;
define Lexicon ~$["+PERF"] .o. Lexicon;
define ContinuousPerf $["+CONT"] .o. Perfect;
define RecentPast $["+RCT.PAST"] .o. Perfect;
define Lexicon ~$["+RCT.PAST"] .o. Lexicon;
define RemotePast $["+RMT.PAST"] .o. Perfect;
define Lexicon ~$["+RMT.PAST"] .o. Lexicon;
define ImmediatePast $["+IMM.PAST"] .o. Perfect;
define Lexicon ~$["+IMM.PAST"] .o. Lexicon;
define Stative $["+PRES.STAT.HAB"] .o. Perfect;
define StativeProg $["+PRES.STAT.PROG"] .o. Perfect;
define Stative Stative | StativeProg;
define Lexicon ~$["+PRES.STAT.HAB"] .o. Lexicon;
define Lexicon ~$["+PRES.STAT.PROG"] .o. Lexicon;
define Past RecentPast | RemotePast;
define Past Past | ImmediatePast;
define Past Past | Stative;
define Past Past | ContinuousPerf;
define Lexicon Lexicon | Past;

# Ensuring guessed verbs have aspects
define Verb $["+V"] .o. Lexicon;
define GuessVerb $["+GUESS"] .o. Verb;
define Lexicon ~$["+V"] .o. Lexicon;
define Verb ~$["+GUESS"] .o. Verb;
define Lexicon Lexicon | Verb;
define Imp $["+IMP"] .o. GuessVerb;
define Perf $["+PERF"] .o. GuessVerb;

# Applying stem change rules to guess actual verb stem from the past stem that occurs in perfect verbs
define TSEnd [t s %@] -> [k e j | k i j | t s];
define SHEnd [s h %@] -> [s h e j | s h i j | s h];
define BEnd [b %@] -> [b y | b];
define DEnd [d %@] -> [z | d];
define GEnd [g %@] -> [z | g];
define HEnd [h %@] -> [s h y | h] || Vowel _;
define KEnd [k %@] -> [t s | k];
define MEnd [m %@] -> [m y | m];
define NEnd [n %@] -> [n y | n n y | n];
define NYEnd [n y %@] -> [n y i j | n y];
define NYWEnd [n y w %@] -> [n y i j w | n y w];
define REnd [r %@] -> [y | z | r e y | r i y | r];
define RWEnd [r w %@] -> [w | z w | r e w | r i w | r w];
define SEnd [s %@] -> [s h | s h e j | s h i j | s] || [Vowel | n] _;
define SHWEnd [s h w %@] -> [s h e j w | s h i j w | s h w];
define SHYEnd [s h y %@] -> [h e j e | h i j e | s h y];
define TEnd [t %@] -> [s | s h | t];
define ZEnd [z %@] -> [j | j e j | j i j | z];
define ZWEnd [z w %@] -> [j w | j e j w| j i j w | z w];

define Perf Perf    .o. 
            TSEnd   .o.
            SHEnd   .o.
            BEnd    .o.
            DEnd    .o.
            GEnd    .o.
            HEnd    .o.
            KEnd    .o.
            MEnd    .o.
            NEnd    .o.
            NYEnd   .o.
            NYWEnd  .o.
            REnd    .o.
            RWEnd   .o.
            SEnd    .o.
            SHWEnd  .o.
            SHYEnd  .o.
            TEnd    .o.
            ZEnd    .o.
            ZWEnd;


define GuessVerb Imp | Perf;
define Lexicon Lexicon | GuessVerb;


# Allows proper nouns not to follow orthographic rules (since they may be foreign)
define Proper $["+PROPER"] .o. Lexicon;
 
define Grammar Lexicon           .o.
	       AItoE			.o.
	       RefI			.o.
	       Cleanup1			.o.
	       VowelHiatus			.o.
	       PrepRule		.o.
               KtoG			.o.
               UtoW       .o.
               ItoY              .o.
               NHtoMP              .o.
                KtoC			.o.
                TtoD			.o.
		NasAssim		.o.
		RtoD			.o.
		YW                      .o.
		BPass;	



define Grammar Grammar | Proper;

define Grammar Grammar .o.
                Cleanup1    .o.
                Cleanup2		.o.
		Cleanup3		.o.
                Cleanup4                .o.
		Cleanup5;			


# English: The following three lines allow any English words to be recognized and tagged as such. You may or may not want this feature; if not, just comment out this section.
# The creator of the word list requires the following to be displayed: Copyright Â© J Ross Beresford 1993-1999. All Rights Reserved. The following restriction is placed on the use of this publication: if the UK Advanced Cryptics Dictionary is used in a software package or redistributed in any form, the copyright notice must be prominently displayed and the text of this document must be included verbatim. There are no other restrictions: I would like to see the list distributed as widely as possible.
read lexc enWords.lexc
define EnglishLexicon;
define Grammar Grammar | EnglishLexicon;

# Uncommenting the next three lines will make it so that, if any analysis exists that is not a guess, no guessed anlyses will be printed. Note that this makes the program take up roughly 80 times as much memory.
#define Guess $["+GUESS"] .o. Grammar;
#define NoGuess ~$["+GUESS"] .o. Grammar;
#define Grammar NoGuess .p. Guess;



regex Grammar;


